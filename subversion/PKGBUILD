# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=subversion
pkgver=1.14.2
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System"
arch=('i686' 'x86_64')
url="https://subversion.apache.org/"
license=('APACHE')
groups=('VCS')
depends=('libsqlite' 'file' 'liblz4' 'libserf' 'libsasl' 'libexpat')
makedepends=('python' 'python-py3c' 'perl' 'swig' 'ruby' 'liblz4-devel' 'libsqlite-devel' 'libserf-devel' 'libsasl-devel' 'gmp-devel')
optdepends=('bash-completion: for svn bash completion'
            'python: for some hook scripts'
            'ruby: for some hook scripts')
provides=('svn')
options=('!makeflags' '!libtool' '!emptydirs')
noextract=("subversion-${pkgver}.tar.bz2")
source=(https://archive.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2{,.asc}
        svnclean
        0001-Create-directories-in-build-tree.patch
        0002-Fix-linking-for-Cygwin.patch
        0003-Fix-linking-for-perl.patch
        0004-Fix-a-switch-test-that-expects-to-an-error-due-to-re.patch
        0005-Use-WIN32-retry-loop-on-Cygwin-too.patch
        0006-Use-binary-mode-for-svnadmin-dump-load.patch
        0007-Don-t-support-DOS-paths-for-Cygwin.patch
        0008-Fix-ruby-tests.patch
        0009-Support-wincrypt-for-password-encryption-on-Cygwin.patch
        0010-Install-perl-modules-into-vendors.patch
        0011-XFAIL-some-case-sensitive-rename-and-move-tests.patch
        0012-Fix-svnlook_tests.py-so-it-sets-PATH-in-the-python-h.patch
        0013-Fix-DSO-open-specifically-for-auth-modules-like-GNOM.patch
        0014-Fix-svnauthz_tests.py-by-adding-usr-bin-to-PATH.patch
        0015-Mark-basic_test.py-rm_missing_with_case_clashing_ond.patch
        0016-Support-swig3.patch
        0017-Fix-serf-config.patch
        0018-Remove-contrib.patch
        0019-subversion-1.9.1-msys2.patch
        0020-remove-checking-symlink.patch
        0021-90-use-copy-instead-symlink.patch
        0022-Avoid-inadvertently-blocking-files-during-svnadmin-c.patch)
sha256sums=('c9130e8d0b75728a66f0e7038fc77052e671830d785b5616aad53b4810d3cc28'
            'SKIP'
            'b09dd041aba0078c8d50df130ef2f96c3ef8486279a620532fef4fe48ef9961e'
            '5ec8ac87efd106a662b91f74bec402341baf00f4c089a5d100da5b8ca35aa5ae'
            '36901adb72fbd1c397bf309f5c529e007bbe2d060d582805284ea00d2a5aed22'
            '70dcb38bc810e76f8d4187ade75a273bf538edce62662e814b88fac5edf6b238'
            '98c1852d11502d8a1c46a3d9dc31ac59f5458946676cdb1446a9f55d8f499b99'
            '008ab11dbccdb594dae9c3b4ca36b6046cc52500d34cbc628176e85325ecb87f'
            '7e3960aa3a9b5647cafd1bc9a53aadedd1b8981a83b16114e294f215fdff7901'
            '73d206e49cd8f2bfe1575061dc51d5174ece43e3845720057ef5f434ed8eac0e'
            '3ef14a85b292cf0ae7e63c0d0bfb98697df993e62ea6317e05a0699b09a0cbbf'
            '468954e8e7c30129f95c7634057cb260544dfe31aea5bf09f36ebf2fd8db4316'
            '5a782cbb3e408a09b1648c148e7029a51b541acf5f709e627f68cd4017ab12f8'
            '3f8eee63c708d80dcf4f890ebc36ab508e4b058296728500d986271e9388cf00'
            '3de47a952de1abeb15f216c966553d2abe27b2113de56dfdc87e5cb87e8bdb66'
            '2be4fd0cb52a00d047667329bf3c0a1a674800037e194d149c640540643583e4'
            '5e8f08e76c8fb7cb93466f6984fd0dfbd9eba0a7c0d68442fa1350ee921118c3'
            '69ec36190c18ca1587797f6c6a68cd70a61407711e527cab321082b78a450c22'
            '689d4a5ebac50fb9378bc18a1a9a0f5c3b5aec235f378a383d3496511506e23e'
            '59bd993a0f24ebd52204b7f7e1e1ee6ad3d5572a911f2cbf93fe5aee1e335251'
            '4a9f031c7eabb1582a165df087133293ee294aa2e578716ee8fc1181f4fbb809'
            '703452c939ce8018f8030e6dde311921edcd8f188296c7ad43e973ae1964fcdb'
            'b4cd9841abb8bfd1f692635cc8c7b84ebae7ad8550f6ffb9cfc867aa11aae209'
            '724eec2b17bd9504b15a485610b00f88f474c275fbcc53bdc0d901c6607d25bb'
            '5cae2e129257a49071ad83aa12e968ebc229bd9ceb147651ff62285f8f5cf96e')
validpgpkeys=('91BFBF4D6956BD5DF7B72D23DFE691AE331BA3DB'
              'BA3C15B1337CF0FB222BD41A1BCA6586A347943F'
              'EC25FCC105618D04ADB43429C4416167349A3BCB')

case "$(uname -m)" in
i686)
# py3c was not built in 32-bit MSYS2; In fact, all 32-bit development of MSYS2 stopped.
# Just skip the Python bindings; Git for Windows does not need them.
makedepends=('perl' 'swig' 'ruby' 'liblz4-devel' 'libsqlite-devel' 'libserf-devel' 'libsasl-devel' 'gmp-devel')
PYTHON_SITELIB=
;;
*)
PYTHON_SITELIB=$(/usr/bin/python -c 'from distutils.sysconfig import * ; print(get_python_lib(0,0));')
;;
esac

prepare() {
  cd ${startdir}/
  [[ -d ${srcdir}/${pkgname}-${pkgver} ]] && rm -rf -d ${srcdir}/${pkgname}-${pkgver}
  tar -xjvf ${SRCDEST}/${pkgname}-${pkgver}.tar.bz2 -C ${srcdir} || true

  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i 's|except IOError,|except IOError as|' build/getversion.py

  patch -p1 -i ${srcdir}/0001-Create-directories-in-build-tree.patch
  patch -p1 -i ${srcdir}/0002-Fix-linking-for-Cygwin.patch
  patch -p1 -i ${srcdir}/0003-Fix-linking-for-perl.patch
  patch -p1 -i ${srcdir}/0004-Fix-a-switch-test-that-expects-to-an-error-due-to-re.patch
  patch -p1 -i ${srcdir}/0005-Use-WIN32-retry-loop-on-Cygwin-too.patch
  patch -p1 -i ${srcdir}/0006-Use-binary-mode-for-svnadmin-dump-load.patch
  patch -p1 -i ${srcdir}/0007-Don-t-support-DOS-paths-for-Cygwin.patch
  patch -p1 -i ${srcdir}/0008-Fix-ruby-tests.patch
  patch -p1 -i ${srcdir}/0009-Support-wincrypt-for-password-encryption-on-Cygwin.patch
  patch -p1 -i ${srcdir}/0010-Install-perl-modules-into-vendors.patch
  patch -p1 -i ${srcdir}/0011-XFAIL-some-case-sensitive-rename-and-move-tests.patch
  patch -p1 -i ${srcdir}/0012-Fix-svnlook_tests.py-so-it-sets-PATH-in-the-python-h.patch
  patch -p1 -i ${srcdir}/0013-Fix-DSO-open-specifically-for-auth-modules-like-GNOM.patch
  patch -p1 -i ${srcdir}/0014-Fix-svnauthz_tests.py-by-adding-usr-bin-to-PATH.patch
  patch -p1 -i ${srcdir}/0015-Mark-basic_test.py-rm_missing_with_case_clashing_ond.patch
  patch -p1 -i ${srcdir}/0016-Support-swig3.patch
  patch -p1 -i ${srcdir}/0017-Fix-serf-config.patch
  patch -p1 -i ${srcdir}/0018-Remove-contrib.patch
  patch -p1 -i ${srcdir}/0019-subversion-1.9.1-msys2.patch
  patch -p1 -i ${srcdir}/0020-remove-checking-symlink.patch
  patch -p1 -i ${srcdir}/0021-90-use-copy-instead-symlink.patch
  patch -p1 -i ${srcdir}/0022-Avoid-inadvertently-blocking-files-during-svnadmin-c.patch

  PYTHON=/usr/bin/python2 ./autogen.sh
  # Hack to allow `libsvn_test-1` to be built
  sed 's/ -o libsvn_test-1.la / -no-undefined&/' -i build-outputs.mk
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  cd ${pkgname}-${pkgver}
  export PYTHON=/usr/bin/python
  ./configure --prefix=/usr \
      --build=${CHOST} \
      --with-apr=/usr \
      --with-apr-util=/usr \
      --with-zlib=/usr \
      --with-serf=/usr \
      --with-sqlite=/usr \
      --without-apxs \
      --without-gnome-keyring \
      --with-utf8proc=internal \
      --enable-shared --disable-static \
      --with-ruby-sitedir=/usr/lib/ruby/vendor_ruby \
      --disable-mod-activation \
      --enable-disallowing-of-undefined-references \
      --enable-local-library-preloading

  test -n "$PYTHON_SITELIB" || {
    sed -i 's/\(swig:.*\) [^ ]*-py/\1/' Makefile
    sed -i 's/\(swig:.*\) [^ ]*-py/\1/' build-outputs.mk
  }

  make clean-swig
  make autogen-swig
  make all

  plain "Compiling swig bindings: ruby"
  make swig-rb

  plain "Compiling swig bindings: perl"
  make swig-pl

  test -z "$PYTHON_SITELIB" || {
  plain "Compiling swig bindings: python"
  make swig-py swig_pydir=${PYTHON_SITELIB}/libsvn swig_pydir_extra=${PYTHON_SITELIB}/svn
  }
}

#check() {
#   cd ${pkgname}-${pkgver}
#   export LANG=C LC_ALL=C
#   make check check-swig-pl check-swig-py check-swig-rb CLEANUP=yes # check-javahl
#}

package() {
  cd ${pkgname}-${pkgver}

  make -j1 DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
    swig_pydir=${PYTHON_SITELIB}/libsvn \
    swig_pydir_extra=${PYTHON_SITELIB}/svn \
    install install-tools $(test -z "$PYTHON_SITELIB" || echo install-swig-py) install-swig-pl install-swig-rb

  install -dm755 "${pkgdir}"/usr/share/subversion
  cp -a tools/hook-scripts "${pkgdir}"/usr/share/subversion/
  rm "${pkgdir}"/usr/share/subversion/hook-scripts/*.in

  ## svnclean
  install -Dm 755 ${srcdir}/svnclean "${pkgdir}"/usr/bin/svnclean

  install -Dm 644 tools/client-side/bash_completion \
     "${pkgdir}"/usr/share/bash-completion/completions/subversion
  for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/usr/share/bash-completion/completions/${i}
  done

  # Remove illegal files
  find ${pkgdir}/usr -type f -name "*::*" | xargs rm -f

  # fix permissons
  find ${pkgdir}/usr -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755
}
